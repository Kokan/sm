/**
 * The grammar defines not only the concrete syntax, but also the underlying
 * semantic (AST) metamodel of our language. After running the 'Generate Language
 * Infrastructure' configuration, you can find the inferred metamodel inside
 * 'src-gen/hu.elte.sm.sM'.
 * 
 * See: https://www.eclipse.org/Xtext/documentation/301_grammarlanguage.html
*/

/**
 * We reuse Xtype for type references and Xbase for statements and expressions.
 * These are built-in sublanguages, of course written in Xtext.
 * 
 * See: https://www.eclipse.org/Xtext/documentation/305_xbase.html#jvmtypes
 * See: https://www.eclipse.org/Xtext/documentation/305_xbase.html#xbase-java-references
 * See: https://www.eclipse.org/Xtext/documentation/305_xbase.html#xbase-expressions
 * See: https://www.eclipse.org/Xtext/documentation/305_xbase.html#xbase-language-ref-introduction
 */

grammar hu.elte.sm.SM with org.eclipse.xtext.xbase.Xbase
generate sM "http://www.elte.hu/sm/SM"

import "http://www.eclipse.org/xtext/xbase/Xbase" as xbase

// Structural Elements

SMFile:
	'package' name=QualifiedName
	importSection=XImportSection?
	machines+=SMMachine*
;

SMMachine:
	'machine' name=ValidID '{'
		members+=SMMachineMember*
	'}'
;

SMMachineMember
	: SMField
	| SMMethod
	| SMEvent
	| SMState
	| SMTransition
;

SMField:
	type=JvmTypeReference name=ValidID
;

SMMethod:
	'def' type=JvmTypeReference name=ValidID '('
		(parameters+=FullJvmFormalParameter (',' parameters+=FullJvmFormalParameter)*)?
	')' body=XBlockExpression
;

SMEvent:
	'event' name=ValidID
;

SMState:
	(init?='init' | 'state') name=ValidID
;

SMTransition:
	'transition' '{'
		'from' source=[SMState]
		'to' target=[SMState]
		'on' trigger=[SMEvent]
	'}'
;

// Statements and Expressions

SMExpression returns xbase::XExpression
	: SMCheckStateExpression
	| SMRaiseEventExpression
;

SMCheckStateExpression returns xbase::XExpression:
	{SMCheckStateExpression}
	'is' target=XExpression 'in' state=[SMState|QualifiedName]
;

SMRaiseEventExpression returns xbase::XExpression:
	{SMRaiseEventExpression}
	'raise' event=[SMEvent|QualifiedName]? 'in' target=XExpression
;

/**
 * Inject our custom action language elements into Xbase.
 */
@Override
XPrimaryExpression returns xbase::XExpression
	: XConstructorCall
	| XBlockExpression
	| XSwitchExpression
	| XSynchronizedExpression
	| XFeatureCall
	| XLiteral
	| XIfExpression
	| XForLoopExpression
	| XBasicForLoopExpression
	| XWhileExpression
	| XDoWhileExpression
	| XThrowExpression
	| XReturnExpression
	| XTryCatchFinallyExpression
	| XParenthesizedExpression
	| SMExpression // the only difference compared to super
;
